Sub CalSharingofPV()

    If Not ActiveSheet.Name = "Security Apportionment" Then
        MsgBox "Please open sheet 'Security Apportionment'", vbExclamation
        Exit Sub
    End If

    Application.ScreenUpdating = False

    Dim PrintRow As Long
    PrintRow = 2

    Dim wsPrint As Worksheet
    Set wsPrint = ThisWorkbook.Sheets("Hide - ECF Report 1")
    wsPrint.Range("A3:N1048576").ClearContents
    wsPrint.Range("B2, E2, F2, J2, H2, L2, M2").ClearContents

    Dim m As Long, n As Long
    m = CountFacility()
    n = CountSecurity()

    Dim BlankRowUpside As Long
    BlankRowUpside = BlankRow_Upside()

    Dim Height As Long
    If m >= 5 Then
        Height = 11 + m
    Else
        Height = 11 + 5
    End If

    Dim startdate As Date
    startdate = Sheets("Start").Range("B3").Value

    Dim DrawnOriginal() As Double, irr() As Double
    ReDim DrawnOriginal(1 To m)
    ReDim irr(1 To m)

    Dim i As Long
    For i = 1 To m
        DrawnOriginal(i) = Sheets("Hide - Facility").Range("L" & (1 + i)).Value
        irr(i) = Sheets("Facility").Range("AK" & (7 + i)).Value
    Next i

    Dim securityWs As Worksheet
    Set securityWs = ThisWorkbook.Sheets("Hide - Security1")

    With securityWs.Sort
        .SortFields.Clear
        .SortFields.Add Key:=securityWs.Range("A1"), Order:=xlDescending
        .SortFields.Add Key:=securityWs.Range("J1"), Order:=xlAscending
        .SetRange securityWs.Range("A:K")
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    Dim scen As Long
    For scen = 1 To 4

        Dim Facility() As Object
        ReDim Facility(1 To m)

        For i = 1 To m
            Set Facility(i) = CreateObject("Scripting.Dictionary")
            Facility(i)("LoanName") = Sheets("Facility").Range("J" & (7 + i)).Value
            Facility(i)("Compoundrate") = 0#
            Facility(i)("RemainosPV") = DrawnOriginal(i)
            Facility(i)("RemainosPVStore") = DrawnOriginal(i)
            Facility(i)("RemainosFV") = 0#
        Next i

        Dim Security() As Object
        ReDim Security(1 To n)

        For i = 1 To n
            Set Security(i) = CreateObject("Scripting.Dictionary")
            Security(i)("SecurityName") = Sheets("Hide - Security1").Range("B" & (1 + i)).Value
            Security(i)("OriginalOrder") = i
            Security(i)("MarketValue") = Sheets("Security Apportionment").Range("K" & (BlankRowUpside + 7 + (i - 1) * Height)).Offset(0, 1 + 3 * (scen - 1)).Value
            Security(i)("Paymentdate") = Sheets("Security Apportionment").Range("K" & (BlankRowUpside + 2 + (i - 1) * Height)).Offset(0, 1 + 3 * (scen - 1)).Value
        Next i

        Dim tmp As Object
        Dim j As Long
        For i = 1 To n - 1
            For j = 1 To n - i
                If Security(j)("Paymentdate") > Security(j + 1)("Paymentdate") Then
                    Set tmp = Security(j)
                    Set Security(j) = Security(j + 1)
                    Set Security(j + 1) = tmp
                End If
            Next j
        Next i

        Dim RankBySecurity() As Integer
        Dim RowPos() As Long
        ReDim RankBySecurity(1 To n, 1 To m)
        ReDim RowPos(1 To n, 1 To m)

        Dim order As Long
        For order = 1 To n

            Dim rangeStr As String
            rangeStr = "D" & (BlankRowUpside + 9 + (Security(order)("OriginalOrder") - 1) * Height) & _
                       ":D" & (BlankRowUpside + 9 + m + (Security(order)("OriginalOrder") - 1) * Height)

            Dim Cell As Range
            Dim facRowOrder As Long
            facRowOrder = 0

            For Each Cell In ThisWorkbook.Worksheets("Security Apportionment").Range(rangeStr)
                facRowOrder = facRowOrder + 1
                For i = 1 To m
                    If Facility(i)("LoanName") = Cell.Value Then
                        RowPos(order, i) = facRowOrder
                        If Cell.Offset(0, 2).Value <> Empty Then
                            RankBySecurity(order, i) = CInt(Cell.Offset(0, 2).Value)
                        Else
                            RankBySecurity(order, i) = 0
                        End If
                        Exit For
                    End If
                Next i
            Next Cell

        Next order

        Dim firstOrder As Long, lastOrder As Long
        Dim dt As Date
        firstOrder = 1

        Do While firstOrder <= n

            dt = Security(firstOrder)("Paymentdate")
            lastOrder = firstOrder

            Do While lastOrder < n
                If Security(lastOrder + 1)("Paymentdate") = dt Then
                    lastOrder = lastOrder + 1
                Else
                    Exit Do
                End If
            Loop

            For order = firstOrder To lastOrder

                For i = 1 To m
                    Facility(i)("RemainosPVStore") = Facility(i)("RemainosPV")
                Next i

                Call EnsureFacilityFVForDate_NoTypes(m, Facility, irr, startdate, Security(order)("Paymentdate"))

                Call AllocateRank1ProRata_SingleSecurity_NoTypes(order, m, Facility, Security, RankBySecurity)

                Call AllocateRank23Waterfall_NoTypes(order, m, Facility, Security, RankBySecurity, RowPos)

                Call UpdatePVFromFV_NoTypes(m, Facility)

                Call PrintSecurityResult_NoTypes(order, scen, m, Facility, Security, RankBySecurity, irr, startdate, BlankRowUpside, Height, wsPrint, PrintRow)

            Next order

            firstOrder = lastOrder + 1

        Loop

    Next scen

    If PrintRow > 2 Then
        wsPrint.Range("A2").AutoFill Destination:=wsPrint.Range("A2:A" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("C2").AutoFill Destination:=wsPrint.Range("C2:C" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("D2").AutoFill Destination:=wsPrint.Range("D2:D" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("G2").AutoFill Destination:=wsPrint.Range("G2:G" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("I2").AutoFill Destination:=wsPrint.Range("I2:I" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("K2").AutoFill Destination:=wsPrint.Range("K2:K" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("N2").AutoFill Destination:=wsPrint.Range("N2:N" & PrintRow), Type:=xlFillDefault
    End If

    With securityWs.Sort
        .SortFields.Clear
        .SortFields.Add Key:=securityWs.Range("J1"), Order:=xlAscending
        .SetRange securityWs.Range("A:K")
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    Application.ScreenUpdating = True

End Sub

Private Sub EnsureFacilityFVForDate_NoTypes(ByVal m As Long, ByRef Facility() As Object, ByRef irr() As Double, ByVal startdate As Date, ByVal dt As Date)

    Dim f As Long

    If dt = 0 Then
        For f = 1 To m
            Facility(f)("Compoundrate") = 1#
            Facility(f)("RemainosFV") = CDbl(Facility(f)("RemainosPV"))
        Next f
        Exit Sub
    End If

    For f = 1 To m
        If CDbl(Facility(f)("Compoundrate")) = 0# Then
            Facility(f)("Compoundrate") = (1 + irr(f)) ^ ((dt - startdate) / 365#)
        End If
        If CDbl(Facility(f)("Compoundrate")) <> 0# Then
            Facility(f)("RemainosFV") = CDbl(Facility(f)("RemainosPV")) * CDbl(Facility(f)("Compoundrate"))
        Else
            Facility(f)("RemainosFV") = CDbl(Facility(f)("RemainosPV"))
        End If
    Next f

End Sub

Private Sub AllocateRank1ProRata_SingleSecurity_NoTypes(ByVal order As Long, ByVal m As Long, _
                                                      ByRef Facility() As Object, ByRef Security() As Object, _
                                                      ByRef RankBySecurity() As Integer)

    Dim mv As Double
    mv = CDbl(Security(order)("MarketValue"))
    If mv <= 0.1 Then Exit Sub

    Do While mv > 0.1

        Dim sumos2 As Double
        sumos2 = 0#

        Dim f As Long
        For f = 1 To m
            If RankBySecurity(order, f) = 1 Then
                Dim pvOutstanding As Double
                If CDbl(Facility(f)("Compoundrate")) <> 0# Then
                    pvOutstanding = CDbl(Facility(f)("RemainosFV")) / CDbl(Facility(f)("Compoundrate"))
                Else
                    pvOutstanding = CDbl(Facility(f)("RemainosFV"))
                End If
                If pvOutstanding > 0# Then sumos2 = sumos2 + pvOutstanding
            End If
        Next f

        If sumos2 <= 0# Then Exit Do

        Dim repayedvalue As Double
        repayedvalue = 0#

        For f = 1 To m

            If RankBySecurity(order, f) = 1 Then

                Dim pvOut As Double
                If CDbl(Facility(f)("Compoundrate")) <> 0# Then
                    pvOut = CDbl(Facility(f)("RemainosFV")) / CDbl(Facility(f)("Compoundrate"))
                Else
                    pvOut = CDbl(Facility(f)("RemainosFV"))
                End If

                If pvOut > 0# Then

                    Dim repayFV As Double
                    repayFV = mv * (pvOut / sumos2)

                    If repayFV > CDbl(Facility(f)("RemainosFV")) Then repayFV = CDbl(Facility(f)("RemainosFV"))
                    If repayFV < 0# Then repayFV = 0#

                    Facility(f)("RemainosFV") = CDbl(Facility(f)("RemainosFV")) - repayFV
                    repayedvalue = repayedvalue + repayFV

                    If CDbl(Facility(f)("RemainosFV")) <= 0.000001 Then Facility(f)("RemainosFV") = 0#

                End If

            End If

        Next f

        If repayedvalue <= 0# Then Exit Do

        mv = mv - repayedvalue
        If mv < 0.000001 Then mv = 0#

    Loop

    Security(order)("MarketValue") = mv

End Sub

Private Sub AllocateRank23Waterfall_NoTypes(ByVal order As Long, ByVal m As Long, _
                                           ByRef Facility() As Object, ByRef Security() As Object, _
                                           ByRef RankBySecurity() As Integer, ByRef RowPos() As Long)

    Dim remainingMV As Double
    remainingMV = CDbl(Security(order)("MarketValue"))

    Dim currentrank As Long
    For currentrank = 2 To 3

        If remainingMV <= 0# Then Exit For

        Dim pos As Long
        For pos = 1 To m

            Dim f As Long
            f = FacilityIndexByRowPos(order, m, RowPos, pos)
            If f <> 0 Then

                If RankBySecurity(order, f) = currentrank Then

                    Dim repayFV As Double
                    repayFV = CDbl(Facility(f)("RemainosFV"))
                    If repayFV > 0# Then

                        If repayFV > remainingMV Then repayFV = remainingMV
                        Facility(f)("RemainosFV") = CDbl(Facility(f)("RemainosFV")) - repayFV
                        remainingMV = remainingMV - repayFV

                        If CDbl(Facility(f)("RemainosFV")) <= 0.000001 Then Facility(f)("RemainosFV") = 0#
                        If remainingMV <= 0# Then Exit For

                    End If

                End If

            End If

        Next pos

    Next currentrank

    If remainingMV < 0.000001 Then remainingMV = 0#
    Security(order)("MarketValue") = remainingMV

End Sub

Private Sub UpdatePVFromFV_NoTypes(ByVal m As Long, ByRef Facility() As Object)

    Dim f As Long
    For f = 1 To m
        If CDbl(Facility(f)("Compoundrate")) <> 0# Then
            Facility(f)("RemainosPV") = CDbl(Facility(f)("RemainosFV")) / CDbl(Facility(f)("Compoundrate"))
        Else
            Facility(f)("RemainosPV") = CDbl(Facility(f)("RemainosFV"))
        End If
        If CDbl(Facility(f)("RemainosPV")) <= 0.000001 Then Facility(f)("RemainosPV") = 0#
    Next f

End Sub

Private Function FacilityIndexByRowPos(ByVal order As Long, ByVal m As Long, ByRef RowPos() As Long, ByVal targetPos As Long) As Long
    Dim f As Long
    For f = 1 To m
        If RowPos(order, f) = targetPos Then
            FacilityIndexByRowPos = f
            Exit Function
        End If
    Next f
    FacilityIndexByRowPos = 0
End Function

Private Sub PrintSecurityResult_NoTypes(ByVal order As Long, ByVal scen As Long, ByVal m As Long, _
                                       ByRef Facility() As Object, ByRef Security() As Object, _
                                       ByRef RankBySecurity() As Integer, ByRef irr() As Double, _
                                       ByVal startdate As Date, ByVal BlankRowUpside As Long, ByVal Height As Long, _
                                       ByRef wsPrint As Worksheet, ByRef PrintRow As Long)

    Dim rangeStr As String
    rangeStr = "D" & (BlankRowUpside + 9 + (Security(order)("OriginalOrder") - 1) * Height) & _
               ":D" & (BlankRowUpside + 9 + m + (Security(order)("OriginalOrder") - 1) * Height)

    Dim Cell As Range
    Dim i As Long

    For Each Cell In ThisWorkbook.Worksheets("Security Apportionment").Range(rangeStr)

        For i = 1 To m

            If Facility(i)("LoanName") = Cell.Value Then

                If Cell.Offset(0, 2).Value = Empty Then
                    Cell.Offset(0, 8 + 0 + 3 * (scen - 1)).Value = 0
                    Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                    Cell.Offset(0, 8 + 2 + 3 * (scen - 1)).Value = 0
                    Exit For
                End If

                Dim allocPV As Double
                allocPV = CDbl(Facility(i)("RemainosPVStore")) - CDbl(Facility(i)("RemainosPV"))
                If allocPV < 0.000001 Then allocPV = 0#

                Cell.Offset(0, 8 + 0 + 3 * (scen - 1)).Value = allocPV

                If Security(order)("Paymentdate") <> "12:00:00 AM" Then
                    If CDbl(Facility(i)("Compoundrate")) = 0# Then
                        Facility(i)("Compoundrate") = (1 + irr(i)) ^ ((Security(order)("Paymentdate") - startdate) / 365#)
                    End If
                    If CDbl(Facility(i)("Compoundrate")) <> 0# Then
                        Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 1 / CDbl(Facility(i)("Compoundrate"))
                    Else
                        Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                    End If
                Else
                    Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                End If

                Cell.Offset(0, 8 + 2 + 3 * (scen - 1)).Value = allocPV * CDbl(Facility(i)("Compoundrate"))

                wsPrint.Range("B" & PrintRow).Value = Facility(i)("LoanName")
                wsPrint.Range("L" & PrintRow).Value = allocPV

                If CDbl(Facility(i)("Compoundrate")) <> 0# Then
                    wsPrint.Range("J" & PrintRow).Value = 1 / CDbl(Facility(i)("Compoundrate"))
                Else
                    wsPrint.Range("J" & PrintRow).Value = 0
                End If

                wsPrint.Range("F" & PrintRow).Value = allocPV * CDbl(Facility(i)("Compoundrate"))
                wsPrint.Range("H" & PrintRow).Value = Security(order)("Paymentdate")
                wsPrint.Range("M" & PrintRow).Value = Security(order)("SecurityName")

                If scen = 1 Then
                    wsPrint.Range("E" & PrintRow).Value = "Upper"
                ElseIf scen = 2 Then
                    wsPrint.Range("E" & PrintRow).Value = "Base"
                ElseIf scen = 3 Then
                    wsPrint.Range("E" & PrintRow).Value = "Lower"
                ElseIf scen = 4 Then
                    wsPrint.Range("E" & PrintRow).Value = "2nd Strategy"
                End If

                PrintRow = PrintRow + 1

                Exit For

            End If

        Next i

    Next Cell

End Sub
