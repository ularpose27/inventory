Option Explicit

Private Type FacilityInfo
    LoanName As String
    RemainosPV As Double
    RemainosPVStore As Double
    RemainosFV As Double
    Compoundrate As Double
    Rank As Long
    Weight As Long
End Type

Private Type SecurityInfo
    SecurityName As String
    OriginalOrder As Long
    MarketValue As Double
    Paymentdate As Date
End Type

Sub CalSharingofPV()

    If Not ActiveSheet.Name = "Security Apportionment" Then
        MsgBox "Please open sheet 'Security Apportionment'", vbExclamation
        Exit Sub
    End If

    Application.ScreenUpdating = False

    'Preparation for PRINT in ECF Report
    Dim PrintRow As Integer
    PrintRow = 2
    Dim wsPrint As Worksheet
    Set wsPrint = ThisWorkbook.Sheets("Hide - ECF Report 1")
    wsPrint.Range("A3:N1048576").ClearContents
    wsPrint.Range("B2, E2, F2, J2, H2, L2, M2").ClearContents

    Dim i As Long, j As Long, k As Long
    Dim m As Long, n As Long
    m = CountFacility()
    n = CountSecurity()

    Dim BlankRowUpside As Long
    BlankRowUpside = BlankRow_Upside()

    Dim Height As Long
    If m >= 5 Then
        Height = 11 + m
    ElseIf m < 5 Then
        Height = 11 + 5
    End If

    Dim startdate As Date
    startdate = Sheets("Start").Range("B3").Value

    Dim DrawnOriginal() As Double, irr() As Double
    ReDim DrawnOriginal(1 To m), irr(1 To m)

    For i = 1 To m
        DrawnOriginal(i) = Sheets("Hide - Facility").Range("L" & (1 + i)).Value
        irr(i) = Sheets("Facility").Range("AK" & (7 + i)).Value
    Next i

    Dim securityWs As Worksheet
    Set securityWs = ThisWorkbook.Sheets("Hide - Security1")

    With securityWs.Sort
        .SortFields.Clear
        .SortFields.Add Key:=securityWs.Range("A1"), Order:=xlDescending
        .SortFields.Add Key:=securityWs.Range("J1"), Order:=xlAscending
        .SetRange securityWs.Range("A:K")
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    Dim scen As Integer
    For scen = 1 To 4

        Dim Facility() As FacilityInfo
        ReDim Facility(1 To m)

        For i = 1 To m
            Facility(i).LoanName = Sheets("Facility").Range("J" & (7 + i)).Value
            Facility(i).Compoundrate = 0
            Facility(i).RemainosPV = DrawnOriginal(i)
            Facility(i).RemainosPVStore = 0
            Facility(i).Rank = 0
            Facility(i).Weight = 0
        Next i

        Dim Security() As SecurityInfo
        ReDim Security(1 To n)

        For i = 1 To n
            Security(i).SecurityName = Sheets("Hide - Security1").Range("B" & (1 + i)).Value
            Security(i).OriginalOrder = i
            Security(i).MarketValue = Sheets("Security Apportionment").Range("K" & (BlankRowUpside + 7 + (i - 1) * Height)).Offset(0, 1 + 3 * (scen - 1)).Value
            Security(i).Paymentdate = Sheets("Security Apportionment").Range("K" & (BlankRowUpside + 2 + (i - 1) * Height)).Offset(0, 1 + 3 * (scen - 1)).Value
        Next i

        Dim tmp As SecurityInfo
        For i = 1 To UBound(Security) - 1
            For j = 1 To UBound(Security) - i
                If Security(j).Paymentdate > Security(j + 1).Paymentdate Then
                    tmp = Security(j)
                    Security(j) = Security(j + 1)
                    Security(j + 1) = tmp
                End If
            Next j
        Next i

        Dim RankBySecurity() As Integer
        ReDim RankBySecurity(1 To n, 1 To m)

        For i = 1 To n
            Dim rangeStr As String
            rangeStr = "D" & (BlankRowUpside + 9 + (Security(i).OriginalOrder - 1) * Height) & _
                       ":D" & (BlankRowUpside + 9 + m + (Security(i).OriginalOrder - 1) * Height)

            Dim Cell As Range
            Dim facRowOrder As Long
            facRowOrder = 0

            For Each Cell In ThisWorkbook.Worksheets("Security Apportionment").Range(rangeStr)
                For j = 1 To m
                    If Facility(j).LoanName = Cell.Value Then
                        If Cell.Offset(0, 2).Value <> Empty Then
                            RankBySecurity(i, j) = CLng(Cell.Offset(0, 2).Value)
                        Else
                            RankBySecurity(i, j) = 0
                        End If
                        Exit For
                    End If
                Next j
            Next Cell
        Next i

        Dim firstOrder As Long, lastOrder As Long
        Dim dt As Date
        firstOrder = 1

        Do While firstOrder <= n

            dt = Security(firstOrder).Paymentdate
            lastOrder = firstOrder

            Do While lastOrder < n
                If Security(lastOrder + 1).Paymentdate = dt Then
                    lastOrder = lastOrder + 1
                Else
                    Exit Do
                End If
            Loop

            Call AllocateRank1SameDay(firstOrder, lastOrder, m, Facility, Security, RankBySecurity, irr, startdate)

            For i = firstOrder To lastOrder
                Call AllocateRank23Waterfall(i, m, Facility, Security, RankBySecurity, irr, startdate)
            Next i

            firstOrder = lastOrder + 1

        Loop

        For i = 1 To n
            Call PrintSecurityResult(i, scen, m, Facility, Security, RankBySecurity, irr, startdate, BlankRowUpside, Height, wsPrint, PrintRow)
        Next i

        For i = 1 To m
            Facility(i).LoanName = Sheets("Facility").Range("J" & (7 + i)).Value
            Facility(i).Compoundrate = 0
            Facility(i).RemainosPV = DrawnOriginal(i)
            Facility(i).RemainosPVStore = 0
            Facility(i).Rank = 0
            Facility(i).Weight = 0
        Next i

    Next scen

    If PrintRow > 2 Then
        wsPrint.Range("A2").AutoFill Destination:=wsPrint.Range("A2:A" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("C2").AutoFill Destination:=wsPrint.Range("C2:C" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("D2").AutoFill Destination:=wsPrint.Range("D2:D" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("G2").AutoFill Destination:=wsPrint.Range("G2:G" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("I2").AutoFill Destination:=wsPrint.Range("I2:I" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("K2").AutoFill Destination:=wsPrint.Range("K2:K" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("N2").AutoFill Destination:=wsPrint.Range("N2:N" & PrintRow), Type:=xlFillDefault
    End If

    With securityWs.Sort
        .SortFields.Clear
        .SortFields.Add Key:=securityWs.Range("J1"), Order:=xlAscending
        .SetRange securityWs.Range("A:K")
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    Application.ScreenUpdating = True

End Sub

Private Sub AllocateRank1SameDay(ByVal firstOrder As Long, ByVal lastOrder As Long, ByVal m As Long, _
                                ByRef Facility() As FacilityInfo, ByRef Security() As SecurityInfo, _
                                ByRef RankBySecurity() As Integer, ByRef irr() As Double, _
                                ByVal startdate As Date)

    Dim groupMV As Double
    Dim order As Long
    groupMV = 0#
    For order = firstOrder To lastOrder
        groupMV = groupMV + Security(order).MarketValue
    Next order

    Dim i As Long, f As Long
    Dim sumos2 As Double
    Dim repayedvalue As Double
    Dim pvOutstanding As Double
    Dim repayFV As Double
    Dim dt As Date
    dt = Security(firstOrder).Paymentdate

    If dt <> 0 Then
        For f = 1 To m
            If Facility(f).Compoundrate = 0 Then
                Facility(f).Compoundrate = (1 + irr(f)) ^ ((dt - startdate) / 365#)
            End If
            If Facility(f).Compoundrate <> 0 Then
                Facility(f).RemainosFV = Facility(f).Compoundrate * Facility(f).RemainosPV
            Else
                Facility(f).RemainosFV = Facility(f).RemainosPV
            End If
        Next f
    End If

    Do While groupMV > 0.1

        sumos2 = 0#
        For f = 1 To m
            If FacilityAppearsAsRank(firstOrder, lastOrder, f, 1, RankBySecurity) Then
                If Facility(f).Compoundrate <> 0 Then
                    sumos2 = sumos2 + (Facility(f).RemainosFV / Facility(f).Compoundrate)
                Else
                    sumos2 = sumos2 + Facility(f).RemainosFV
                End If
            End If
        Next f

        If sumos2 <= 0# Then Exit Do

        repayedvalue = 0#

        For f = 1 To m
            If FacilityAppearsAsRank(firstOrder, lastOrder, f, 1, RankBySecurity) Then

                If Facility(f).Compoundrate <> 0 Then
                    pvOutstanding = Facility(f).RemainosFV / Facility(f).Compoundrate
                Else
                    pvOutstanding = Facility(f).RemainosFV
                End If

                repayFV = groupMV * (pvOutstanding / sumos2)
                If repayFV > Facility(f).RemainosFV Then repayFV = Facility(f).RemainosFV
                If repayFV < 0# Then repayFV = 0#

                Facility(f).RemainosFV = Facility(f).RemainosFV - repayFV
                repayedvalue = repayedvalue + repayFV

                If Facility(f).RemainosFV <= 0.000001 Then
                    Facility(f).RemainosFV = 0#
                End If

            End If
        Next f

        If repayedvalue <= 0# Then Exit Do

        Dim ratio As Double
        ratio = repayedvalue / groupMV
        If ratio > 1# Then ratio = 1#
        If ratio < 0# Then ratio = 0#

        For order = firstOrder To lastOrder
            Security(order).MarketValue = Security(order).MarketValue - (Security(order).MarketValue * ratio)
            If Security(order).MarketValue < 0.000001 Then Security(order).MarketValue = 0#
        Next order

        groupMV = 0#
        For order = firstOrder To lastOrder
            groupMV = groupMV + Security(order).MarketValue
        Next order

    Loop

End Sub

Private Sub AllocateRank23Waterfall(ByVal order As Long, ByVal m As Long, _
                                   ByRef Facility() As FacilityInfo, ByRef Security() As SecurityInfo, _
                                   ByRef RankBySecurity() As Integer, ByRef irr() As Double, _
                                   ByVal startdate As Date)

    Dim currentrank As Long
    Dim remainingMV As Double
    Dim f As Long
    Dim repayFV As Double

    remainingMV = Security(order).MarketValue

    For currentrank = 2 To 3

        If remainingMV <= 0# Then Exit For

        For f = 1 To m

            If RankBySecurity(order, f) = currentrank Then

                If remainingMV <= 0# Then Exit For

                repayFV = Facility(f).RemainosFV
                If repayFV <= 0# Then GoTo NextFacilityR23
                If repayFV > remainingMV Then repayFV = remainingMV

                Facility(f).RemainosFV = Facility(f).RemainosFV - repayFV
                remainingMV = remainingMV - repayFV

                If Facility(f).RemainosFV <= 0.000001 Then Facility(f).RemainosFV = 0#

            End If

NextFacilityR23:
        Next f

    Next currentrank

    Security(order).MarketValue = remainingMV

End Sub

Private Function FacilityAppearsAsRank(ByVal firstOrder As Long, ByVal lastOrder As Long, ByVal facIndex As Long, _
                                      ByVal targetRank As Long, ByRef RankBySecurity() As Integer) As Boolean
    Dim o As Long
    For o = firstOrder To lastOrder
        If RankBySecurity(o, facIndex) = targetRank Then
            FacilityAppearsAsRank = True
            Exit Function
        End If
    Next o
    FacilityAppearsAsRank = False
End Function

Private Sub PrintSecurityResult(ByVal order As Long, ByVal scen As Integer, ByVal m As Long, _
                               ByRef Facility() As FacilityInfo, ByRef Security() As SecurityInfo, _
                               ByRef RankBySecurity() As Integer, ByRef irr() As Double, _
                               ByVal startdate As Date, ByVal BlankRowUpside As Long, ByVal Height As Long, _
                               ByRef wsPrint As Worksheet, ByRef PrintRow As Integer)

    Dim rangeStr As String
    rangeStr = "D" & (BlankRowUpside + 9 + (Security(order).OriginalOrder - 1) * Height) & _
               ":D" & (BlankRowUpside + 9 + m + (Security(order).OriginalOrder - 1) * Height)

    Dim Cell As Range
    Dim i As Long

    For Each Cell In ThisWorkbook.Worksheets("Security Apportionment").Range(rangeStr)

        For i = 1 To m

            If Facility(i).LoanName = Cell.Value Then

                If Cell.Offset(0, 2).Value = Empty Then
                    Cell.Offset(0, 8 + 0 + 3 * (scen - 1)).Value = 0
                    Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                    Cell.Offset(0, 8 + 2 + 3 * (scen - 1)).Value = 0
                    Exit For
                End If

                Cell.Offset(0, 8 + 0 + 3 * (scen - 1)).Value = Facility(i).RemainosPVStore - Facility(i).RemainosPV

                If Security(order).Paymentdate <> "12:00:00 AM" Then

                    If Facility(i).Compoundrate = 0 Then
                        Facility(i).Compoundrate = (1 + irr(i)) ^ ((Security(order).Paymentdate - startdate) / 365#)
                    End If

                    'only dividing if we have a valid date
                    If Facility(i).Compoundrate <> 0 Then
                        Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 1 / Facility(i).Compoundrate
                    Else
                        Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                    End If

                Else
                    Cell.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                End If

                Cell.Offset(0, 8 + 2 + 3 * (scen - 1)).Value = (Facility(i).RemainosPVStore - Facility(i).RemainosPV) * Facility(i).Compoundrate

                'Print in ECF Report 1
                wsPrint.Range("B" & PrintRow).Value = Facility(i).LoanName
                wsPrint.Range("L" & PrintRow).Value = Facility(i).RemainosPVStore - Facility(i).RemainosPV

                If Facility(i).Compoundrate <> 0 Then
                    wsPrint.Range("J" & PrintRow).Value = 1 / Facility(i).Compoundrate
                Else
                    wsPrint.Range("J" & PrintRow).Value = 0
                End If

                wsPrint.Range("F" & PrintRow).Value = (Facility(i).RemainosPVStore - Facility(i).RemainosPV) * Facility(i).Compoundrate
                wsPrint.Range("H" & PrintRow).Value = Security(order).Paymentdate
                wsPrint.Range("M" & PrintRow).Value = Security(order).SecurityName

                If scen = 1 Then
                    wsPrint.Range("E" & PrintRow).Value = "Upper"
                ElseIf scen = 2 Then
                    wsPrint.Range("E" & PrintRow).Value = "Base"
                ElseIf scen = 3 Then
                    wsPrint.Range("E" & PrintRow).Value = "Lower"
                ElseIf scen = 4 Then
                    wsPrint.Range("E" & PrintRow).Value = "2nd Strategy"
                End If

                PrintRow = PrintRow + 1

                Exit For

            End If

        Next i

    Next Cell

End Sub
