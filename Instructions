Sub AllocateRank1SameDay(ByVal firstOrder As Long, ByVal lastOrder As Long, ByVal m As Long, Facility() As FacilityInfo, Security() As SecurityInfo, RankBySecurity() As Integer, ByVal groupMV As Double)

    Dim i As Long, f As Long
    Dim sumos2 As Double
    Dim pvOutstanding As Double
    Dim repayFV As Double
    Dim repayedvalue As Double

    Dim idx() As Long
    Dim sortCount As Long
    Dim a As Long, b As Long
    Dim tmpIdx As Long

    sortCount = 0
    ReDim idx(1 To m)

    For f = 1 To m
        For i = firstOrder To lastOrder
            If RankBySecurity(i, f) = 1 Then
                sortCount = sortCount + 1
                idx(sortCount) = f
                Exit For
            End If
        Next i
    Next f

    If sortCount = 0 Then Exit Sub

    If sortCount > 1 Then
        For a = 1 To sortCount - 1
            For b = a + 1 To sortCount
                If Facility(idx(b)).Weight < Facility(idx(a)).Weight Then
                    tmpIdx = idx(a)
                    idx(a) = idx(b)
                    idx(b) = tmpIdx
                End If
            Next b
        Next a
    End If

    Do While groupMV > 0.1

        sumos2 = 0#
        For i = 1 To sortCount
            If Facility(idx(i)).Compoundrate <> 0 Then
                sumos2 = sumos2 + (Facility(idx(i)).RemainosFV / Facility(idx(i)).Compoundrate)
            Else
                sumos2 = sumos2 + Facility(idx(i)).RemainosFV
            End If
        Next i

        If sumos2 <= 0# Then Exit Do

        repayedvalue = 0#

        For i = 1 To sortCount

            If Facility(idx(i)).Compoundrate <> 0 Then
                pvOutstanding = Facility(idx(i)).RemainosFV / Facility(idx(i)).Compoundrate
            Else
                pvOutstanding = Facility(idx(i)).RemainosFV
            End If

            repayFV = groupMV * (pvOutstanding / sumos2)

            If repayFV > Facility(idx(i)).RemainosFV Then repayFV = Facility(idx(i)).RemainosFV

            Facility(idx(i)).RemainosFV = Facility(idx(i)).RemainosFV - repayFV
            repayedvalue = repayedvalue + repayFV

            If Facility(idx(i)).RemainosFV <= 0.000001 Then
                Facility(idx(i)).RemainosFV = 0#
            End If

        Next i

        groupMV = groupMV - repayedvalue
        If repayedvalue <= 0# Then Exit Do

    Loop

End Sub


Sub AllocateRank23Waterfall(ByVal order As Long, ByVal m As Long, Facility() As FacilityInfo, Security() As SecurityInfo, RankBySecurity() As Integer)

    Dim currentrank As Long
    Dim i As Long
    Dim remainingMV As Double
    Dim repayFV As Double

    Dim idx() As Long
    Dim sortCount As Long
    Dim a As Long, b As Long
    Dim tmpIdx As Long

    remainingMV = Security(order).MarketValue

    For currentrank = 2 To 3

        sortCount = 0
        ReDim idx(1 To m)

        For i = 1 To m
            If RankBySecurity(order, i) = currentrank Then
                sortCount = sortCount + 1
                idx(sortCount) = i
            End If
        Next i

        If sortCount = 0 Then GoTo SkipToNextIteration

        If sortCount > 1 Then
            For a = 1 To sortCount - 1
                For b = a + 1 To sortCount
                    If Facility(idx(b)).Weight < Facility(idx(a)).Weight Then
                        tmpIdx = idx(a)
                        idx(a) = idx(b)
                        idx(b) = tmpIdx
                    End If
                Next b
            Next a
        End If

        For i = 1 To sortCount

            If remainingMV <= 0# Then Exit For

            repayFV = Facility(idx(i)).RemainosFV
            If repayFV <= 0# Then GoTo NextFacilityR23
            If repayFV > remainingMV Then repayFV = remainingMV

            Facility(idx(i)).RemainosFV = Facility(idx(i)).RemainosFV - repayFV
            remainingMV = remainingMV - repayFV

NextFacilityR23:
        Next i

SkipToNextIteration:
    Next currentrank

    Security(order).MarketValue = remainingMV

End Sub
