Sub CalSharingofPV()

    If Not ActiveSheet.Name = "Security Apportionment" Then
        MsgBox "Please open sheet 'Security Apportionment'", vbExclamation
        Exit Sub
    End If

    Application.ScreenUpdating = False

    'Preparation for PRINT in ECF Report
    Dim PrintRow As Integer
    PrintRow = 2

    Dim wsPrint As Worksheet
    Set wsPrint = ThisWorkbook.Sheets("Hide - ECF Report 1")
    wsPrint.Range("A3:N1048576").ClearContents
    wsPrint.Range("B2, E2, F2, J2, H2, L2, M2").ClearContents

    Dim i As Integer, j As Integer, k As Integer

    Dim m As Long, n As Long
    m = CountFacility()
    n = CountSecurity()

    Dim BlankRowUpside As Long
    BlankRowUpside = BlankRow_Upside()

    Dim Height As Long
    If m >= 5 Then
        Height = 11 + m
    ElseIf m < 5 Then
        Height = 11 + 5
    End If

    Dim startdate As Date
    startdate = Sheets("Start").Range("B3").Value

    Dim DrawnOriginal(), irr() As Double
    ReDim DrawnOriginal(1 To m), irr(1 To m) As Double

    For i = 1 To m
        DrawnOriginal(i) = Sheets("Hide - Facility").Range("L" & (1 + i)).Value
        irr(i) = Sheets("Facility").Range("AK" & (7 + i)).Value
    Next i

    Dim securityWs As Worksheet
    Set securityWs = ThisWorkbook.Sheets("Hide - Security1")

    With securityWs.Sort
        .SortFields.Clear
        .SortFields.Add key:=securityWs.Range("A1"), order:=xlDescending
        .SortFields.Add key:=securityWs.Range("I1"), order:=xlAscending
        .SetRange securityWs.Range("A:K")
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    Dim scen As Integer
    For scen = 1 To 4

        ReDim Facility(1 To m) As FacilityInfo
        For i = 1 To m
            Facility(i).LoanName = Sheets("Facility").Range("J" & (7 + i)).Value 'Fac Col J
            Facility(i).Compoundrate = 0
            Facility(i).RemainosPV = DrawnOriginal(i)
            Facility(i).RemainosPVStore = 0
            Facility(i).Rank = 0
            Facility(i).Weight = 0
        Next i

        ReDim Security(1 To n) As SecurityInfo
        For i = 1 To n
            Security(i).SecurityName = Sheets("Hide - Security1").Range("B" & (1 + i)).Value
            Security(i).OriginalOrder = i
            Security(i).MarketValue = Sheets("Security Apportionment").Range("K" & (BlankRowUpside + 7 + (i - 1) * Height)).Offset(0, 1 + 3 * (scen - 1)).Value
            Security(i).Paymentdate = Sheets("Security Apportionment").Range("K" & (BlankRowUpside + 2 + (i - 1) * Height)).Offset(0, 1 + 3 * (scen - 1)).Value
        Next i

        Dim temp As SecurityInfo
        For i = 1 To UBound(Security) - 1
            For j = 1 To UBound(Security) - i
                If Security(j).Paymentdate > Security(j + 1).Paymentdate Then
                    temp = Security(j)
                    Security(j) = Security(j + 1)
                    Security(j + 1) = temp
                End If
            Next j
        Next i

        Dim RankBySecurity() As Integer
        Dim WeightBySecurity() As Long
        ReDim RankBySecurity(1 To n, 1 To m) As Integer
        ReDim WeightBySecurity(1 To n, 1 To m) As Long

        Dim order As Long
        For order = 1 To n

            Dim Cell As Range
            Dim rangeStr As String

            rangeStr = "D" & (BlankRowUpside + 9 + (Security(order).OriginalOrder - 1) * Height) _
                     & ":D" & (BlankRowUpside + 9 + m + (Security(order).OriginalOrder - 1) * Height)

            Dim facRowOrder As Long
            facRowOrder = 0

            For Each Cell In ThisWorkbook.Worksheets("Security Apportionment").Range(rangeStr)

                For i = 1 To m
                    If Facility(i).LoanName = Cell.Value Then

                        If Cell.Offset(0, 2).Value <> Empty Then
                            facRowOrder = facRowOrder + 1
                            RankBySecurity(order, i) = Cell.Offset(0, 2).Value
                            WeightBySecurity(order, i) = facRowOrder
                        Else
                            RankBySecurity(order, i) = 0
                            WeightBySecurity(order, i) = 0
                        End If

                        Exit For
                    End If
                Next i

            Next Cell

        Next order

        order = 1
        Do While order <= n

            Dim firstOrder As Long, lastOrder As Long
            firstOrder = order
            lastOrder = order

            Do While lastOrder < n
                If Security(lastOrder + 1).Paymentdate = Security(firstOrder).Paymentdate Then
                    lastOrder = lastOrder + 1
                Else
                    Exit Do
                End If
            Loop

            For i = 1 To m
                Facility(i).RemainosPVStore = Facility(i).RemainosPV
            Next i

            Call AllocateRank1SameDayPV(firstOrder, lastOrder, m, Facility, Security, RankBySecurity, irr, startdate)

            Dim o As Long
            For o = firstOrder To lastOrder
                Call AllocateRank23WaterfallPV(o, m, Facility, Security, RankBySecurity, WeightBySecurity, irr, startdate)
            Next o

            order = lastOrder + 1

        Loop

        'PRINT in Security Apportionment
        For order = 1 To n

            Dim rangeStrP As String
            rangeStrP = "D" & (BlankRowUpside + 9 + (Security(order).OriginalOrder - 1) * Height) _
                      & ":D" & (BlankRowUpside + 9 + m + (Security(order).OriginalOrder - 1) * Height)

            Dim CellP As Range
            For Each CellP In ThisWorkbook.Worksheets("Security Apportionment").Range(rangeStrP)

                For i = 1 To m

                    If Facility(i).LoanName = CellP.Value Then

                        If CellP.Offset(0, 2).Value = Empty Then
                            CellP.Offset(0, 8 + 0 + 3 * (scen - 1)).Value = 0
                            CellP.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                            CellP.Offset(0, 8 + 2 + 3 * (scen - 1)).Value = 0
                            Exit For
                        End If

                        CellP.Offset(0, 8 + 0 + 3 * (scen - 1)).Value = Facility(i).RemainosPVStore - Facility(i).RemainosPV

                        Dim thisComp As Double
                        If Security(order).Paymentdate <> "12:00:00 AM" Then
                            thisComp = (1 + irr(i)) ^ ((Security(order).Paymentdate - startdate) / 365)
                            If thisComp <> 0 Then
                                CellP.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 1 / thisComp
                            Else
                                CellP.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                            End If
                        Else
                            thisComp = 0
                            CellP.Offset(0, 8 + 1 + 3 * (scen - 1)).Value = 0
                        End If

                        CellP.Offset(0, 8 + 2 + 3 * (scen - 1)).Value = (Facility(i).RemainosPVStore - Facility(i).RemainosPV) * thisComp

                        'Print in ECF Report 1
                        wsPrint.Range("B" & PrintRow).Value = Facility(i).LoanName
                        wsPrint.Range("L" & PrintRow).Value = Facility(i).RemainosPVStore - Facility(i).RemainosPV

                        If thisComp <> 0 Then
                            wsPrint.Range("J" & PrintRow).Value = 1 / thisComp
                        Else
                            wsPrint.Range("J" & PrintRow).Value = 0
                        End If

                        wsPrint.Range("F" & PrintRow).Value = (Facility(i).RemainosPVStore - Facility(i).RemainosPV) * thisComp
                        wsPrint.Range("H" & PrintRow).Value = Security(order).Paymentdate
                        wsPrint.Range("M" & PrintRow).Value = Security(order).SecurityName

                        If scen = 1 Then
                            wsPrint.Range("E" & PrintRow).Value = "Upper"
                        ElseIf scen = 2 Then
                            wsPrint.Range("E" & PrintRow).Value = "Base"
                        ElseIf scen = 3 Then
                            wsPrint.Range("E" & PrintRow).Value = "Lower"
                        ElseIf scen = 4 Then
                            wsPrint.Range("E" & PrintRow).Value = "2nd Strategy"
                        End If

                        PrintRow = PrintRow + 1
                        Exit For

                    End If

                Next i

            Next CellP

        Next order

        For i = 1 To m
            Facility(i).Compoundrate = 0
            Facility(i).Rank = 0
            Facility(i).Weight = 0
        Next i

    Next scen

    'PRINT in ECF Report
    If PrintRow > 2 Then
        wsPrint.Range("A2").AutoFill Destination:=wsPrint.Range("A2:A" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("C2").AutoFill Destination:=wsPrint.Range("C2:C" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("D2").AutoFill Destination:=wsPrint.Range("D2:D" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("G2").AutoFill Destination:=wsPrint.Range("G2:G" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("I2").AutoFill Destination:=wsPrint.Range("I2:I" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("K2").AutoFill Destination:=wsPrint.Range("K2:K" & PrintRow), Type:=xlFillDefault
        wsPrint.Range("N2").AutoFill Destination:=wsPrint.Range("N2:N" & PrintRow), Type:=xlFillDefault
    Else
    End If

    With securityWs.Sort
        .SortFields.Clear
        .SortFields.Add key:=securityWs.Range("J1"), order:=xlAscending
        .SetRange securityWs.Range("A:K")
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    Application.ScreenUpdating = True

End Sub


Private Sub AllocateRank1SameDayPV(ByVal firstOrder As Long, ByVal lastOrder As Long, ByVal m As Long, _
                                   ByRef Facility() As FacilityInfo, ByRef Security() As SecurityInfo, _
                                   ByRef RankBySecurity() As Integer, _
                                   ByRef irr() As Double, ByVal startdate As Date)

    Dim i As Long, o As Long

    Dim totalMV As Double
    totalMV = 0#

    Dim origMV() As Double
    ReDim origMV(firstOrder To lastOrder) As Double

    For o = firstOrder To lastOrder
        origMV(o) = Security(o).MarketValue
        totalMV = totalMV + Security(o).MarketValue
    Next o

    If totalMV <= 0# Then Exit Sub

    Dim payDate As Date
    payDate = Security(firstOrder).Paymentdate

    Dim comp() As Double
    Dim FV() As Double
    ReDim comp(1 To m) As Double
    ReDim FV(1 To m) As Double

    For i = 1 To m
        comp(i) = (1 + irr(i)) ^ ((payDate - startdate) / 365)
        FV(i) = Facility(i).RemainosPV * comp(i)
    Next i

    Dim inRank1() As Boolean
    ReDim inRank1(1 To m) As Boolean

    For o = firstOrder To lastOrder
        For i = 1 To m
            If RankBySecurity(o, i) = 1 Then
                inRank1(i) = True
            End If
        Next i
    Next o

    Dim remainingPoolMV As Double
    remainingPoolMV = totalMV

    'RANK 1: PRO-RATA
    Do While remainingPoolMV > 0.1

        Dim sumos2 As Double
        sumos2 = 0#

        For i = 1 To m
            If inRank1(i) Then
                If FV(i) > 0# Then
                    If comp(i) <> 0 Then
                        sumos2 = sumos2 + (FV(i) / comp(i))
                    Else
                        sumos2 = sumos2 + FV(i)
                    End If
                End If
            End If
        Next i

        If sumos2 <= 0# Then Exit Do

        Dim repayedvalue As Double
        repayedvalue = 0#

        For i = 1 To m

            If inRank1(i) Then

                If FV(i) > 0# Then

                    Dim pvOutstanding As Double
                    If comp(i) <> 0 Then
                        pvOutstanding = FV(i) / comp(i)
                    Else
                        pvOutstanding = FV(i)
                    End If

                    Dim repayFV As Double
                    repayFV = remainingPoolMV * (pvOutstanding / sumos2)

                    If repayFV > FV(i) Then repayFV = FV(i)

                    FV(i) = FV(i) - repayFV
                    repayedvalue = repayedvalue + repayFV

                    If FV(i) <= 0.000001 Then
                        FV(i) = 0#
                    End If

                End If

            End If

        Next i

        If repayedvalue <= 0# Then Exit Do

        remainingPoolMV = remainingPoolMV - repayedvalue

    Loop

    If remainingPoolMV < 0# Then remainingPoolMV = 0#

    For i = 1 To m
        If comp(i) <> 0 Then
            Facility(i).RemainosPV = FV(i) / comp(i)
        Else
            Facility(i).RemainosPV = FV(i)
        End If
    Next i

    If remainingPoolMV <= 0# Then
        For o = firstOrder To lastOrder
            Security(o).MarketValue = 0#
        Next o
        Exit Sub
    End If

    For o = firstOrder To lastOrder
        Security(o).MarketValue = remainingPoolMV * (origMV(o) / totalMV)
    Next o

End Sub


Private Sub AllocateRank23WaterfallPV(ByVal order As Long, ByVal m As Long, _
                                      ByRef Facility() As FacilityInfo, ByRef Security() As SecurityInfo, _
                                      ByRef RankBySecurity() As Integer, ByRef WeightBySecurity() As Long, _
                                      ByRef irr() As Double, ByVal startdate As Date)

    Dim remainingMV As Double
    remainingMV = Security(order).MarketValue
    If remainingMV <= 0# Then Exit Sub

    Dim payDate As Date
    payDate = Security(order).Paymentdate

    Dim comp() As Double
    Dim FV() As Double
    ReDim comp(1 To m) As Double
    ReDim FV(1 To m) As Double

    Dim i As Long
    For i = 1 To m
        comp(i) = (1 + irr(i)) ^ ((payDate - startdate) / 365)
        FV(i) = Facility(i).RemainosPV * comp(i)
    Next i

    'RANK 2 & 3: WATERFALL
    Dim a As Long, b As Long
    Dim idx() As Long
    Dim sortCount As Long
    Dim tmpIdx As Long

    sortCount = 0
    ReDim idx(1 To m)

    For i = 1 To m
        If RankBySecurity(order, i) = 2 Or RankBySecurity(order, i) = 3 Then
            sortCount = sortCount + 1
            idx(sortCount) = i
        End If
    Next i

    If sortCount > 1 Then
        For a = 1 To sortCount - 1
            For b = a + 1 To sortCount
                If RankBySecurity(order, idx(b)) < RankBySecurity(order, idx(a)) Then
                    tmpIdx = idx(a)
                    idx(a) = idx(b)
                    idx(b) = tmpIdx
                ElseIf RankBySecurity(order, idx(b)) = RankBySecurity(order, idx(a)) Then
                    If WeightBySecurity(order, idx(b)) < WeightBySecurity(order, idx(a)) Then
                        tmpIdx = idx(a)
                        idx(a) = idx(b)
                        idx(b) = tmpIdx
                    End If
                End If
            Next b
        Next a
    End If

    For i = 1 To sortCount

        Dim fIndex As Long
        fIndex = idx(i)

        If remainingMV <= 0# Then Exit For

        If FV(fIndex) > 0# Then

            Dim repayFV As Double
            repayFV = FV(fIndex)
            If repayFV > remainingMV Then repayFV = remainingMV

            FV(fIndex) = FV(fIndex) - repayFV
            remainingMV = remainingMV - repayFV

            If FV(fIndex) <= 0.000001 Then
                FV(fIndex) = 0#
            End If

        End If

    Next i

    For i = 1 To m
        If comp(i) <> 0 Then
            Facility(i).RemainosPV = FV(i) / comp(i)
        Else
            Facility(i).RemainosPV = FV(i)
        End If
    Next i

    Security(order).MarketValue = remainingMV

End Sub
